@name XboxCarControllerUnigearSingleEngine
@inputs Seat:entity Engine:entity Gearbox:entity EGP:wirelink TotalFuel
@outputs A D Active W S Driver:entity 
@persist Gear Throttle Mode:string UpClick DownClick
@trigger 

runOnTick(1)
if(first())
{
    Gear = 1
    Mode = "Manual"
}

Driver = Seat:driver()

A = Driver:keyPressed("xstick1_left")
D = Driver:keyPressed("xstick1_right")
W = Driver:keyPressed("xbutton_rtrigger")
S = Driver:keyPressed("xbutton_ltrigger")
LBumper = Driver:keyPressed("xbutton_left_shoulder")
RBumper = Driver:keyPressed("xbutton_right_shoulder")
ThrottleUp = Driver:keyPressed("xbutton_a")
ThrottleDown = Driver:keyPressed("xbutton_b")
Manual = Driver:keyPressed("xbutton_up")
Automatic = Driver:keyPressed("xbutton_down")

Speed = round(toUnit("mi/h",entity():vel():length()))

if(Driver)
{
    Active = 1
    Engine:acfActive(1)
}
else
{
    Active = 0
    Engine:acfActive(0)
}

if(Manual)
{
    Mode = "Manual"
}
elseif(Automatic)
{
    Mode = "Automatic"
}

if(Mode == "Automatic")
{
    if(Engine:acfRPM() > Engine:acfPowerbandMax() & Gear < Gearbox:acfNumGears() - 1 & Gear != Gearbox:acfNumGears() & UpClick > 75 & W)
    {
        Gear += 1
        UpClick = 0
    }
    elseif((Engine:acfRPM() < Engine:acfPowerbandMin() | !W) & Gear < 1 & DownClick > 75)
    {
        Gear -= 1
        DownClick  = 0
    }
    UpClick += 1
    DownClick += 1
}
elseif(Mode == "Manual")
{
    if(changed(RBumper) & RBumper & Gear < 7)
    {
        Gear += 1
    }
    elseif(changed(LBumper) & LBumper & Gear > 1)
    {
        Gear -= 1
    }
}

if(ThrottleUp & Throttle < 100)
{
    Throttle += 1
}
elseif(ThrottleDown & Throttle > 0)
{
    Throttle -= 1
}

if(W)
{
    Gearbox:acfShift(Gear)
    Engine:acfThrottle(Throttle)
}
elseif(S)
{
    Gearbox:acfShift(Gearbox:acfNumGears())
    if(Mode == "Automatic")
    {
        Gearbox:acfShift(1)
    }
    Engine:acfThrottle(Throttle)
}
else
{
    Gearbox:acfShift(0)
    Engine:acfThrottle(0)
}

Center = egpScrSize(Driver)/2

if(->TotalFuel)
{
    Fuel = " Fuel: " + (round(TotalFuel,2)):toString()
}
else
{
    Fuel = ""
}

EGP:egpLine(1,Center + vec2(-10,-10),Center + vec2(10,10))   
EGP:egpLine(2,Center + vec2(-10,10),Center + vec2(10,-10))
EGP:egpText(3,"Gear:" + Gear:toString() + " Throttle: " + Throttle:toString() + " Speed: " + Speed:toString() + Fuel,Center + vec2(20,-20))
EGP:egpText(4,"Mode: " + Mode,Center + vec2(20,20)) 
 
